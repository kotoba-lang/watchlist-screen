(ns watchlist.adapters.edn-index-test
  (:require [clojure.java.io :as io]
            [clojure.test :refer [deftest is]]
            [watchlist.adapters.edn-index :as edn-index]
            [watchlist.model :as model]
            [watchlist.ports :as ports]))

(defn- temp-dir! []
  (let [f (io/file (System/getProperty "java.io.tmpdir")
                    (str "watchlist-edn-index-test-" (System/nanoTime)))]
    (.mkdirs f)
    f))

(defn- write-source! [dir source entities manifest]
  (spit (io/file dir (str (name source) ".entities.edn")) (pr-str entities))
  (spit (io/file dir (str (name source) ".manifest.edn")) (pr-str manifest)))

(deftest edn-index-loads-entities-and-manifests-across-sources
  (let [dir (temp-dir!)
        e1 (model/entity "ofac-sdn:1" :ofac-sdn {:type :individual :primary-name "A"})
        e2 (model/entity "un-consolidated:1" :un-consolidated {:type :entity :primary-name "B"})
        m1 (model/list-manifest :ofac-sdn {:fetched-at 1 :entity-count 1})
        m2 (model/list-manifest :un-consolidated {:fetched-at 2 :entity-count 1})]
    (write-source! dir :ofac-sdn [e1] m1)
    (write-source! dir :un-consolidated [e2] m2)
    (let [index (edn-index/edn-index (str dir))]
      (is (= #{"ofac-sdn:1" "un-consolidated:1"} (set (map :entity/id (ports/entities index)))))
      (is (= #{:ofac-sdn :un-consolidated} (set (map :manifest/source (ports/manifests index))))))))

(deftest edn-index-missing-source-files-yields-empty-not-an-error
  (let [dir (temp-dir!)
        index (edn-index/edn-index (str dir) #{:ofac-sdn})]
    (is (= [] (ports/entities index)))
    (is (= [] (ports/manifests index)))))

(deftest load-source-returns-empty-shape-when-files-absent
  (let [dir (temp-dir!)
        loaded (edn-index/load-source (str dir) :ofac-sdn)]
    (is (= [] (:entities loaded)))
    (is (nil? (:manifest loaded)))))
